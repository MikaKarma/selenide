test {
  systemProperties['selenide.browser'] = 'htmlunit'
  systemProperties['selenide.reportsFolder'] = 'build/reports/tests/htmlunit'
  include 'integration/**/*FrameWaitTest*'
  exclude 'com/codeborne/selenide/**/*'
}

tasks.withType(Test) {
  useJUnitPlatform()
  systemProperty('file.encoding', encoding)
  systemProperty('BUILD_URL', System.getenv()['BUILD_URL'])
  if (System.getenv('CI')) {
    maxParallelForks = 1
  } else {
    maxParallelForks = (int) Runtime.runtime.availableProcessors().intdiv(2) ?: 1
  }
  jvmArgs = ['-ea', '-Xmx512m']
  systemProperties += System.properties

  classpath += project.sourceSets.staticFacade.output.classesDirs
  classpath += project.sourceSets.staticFacadeTest.output.classesDirs
  testClassesDirs += project.sourceSets.staticFacadeTest.output.classesDirs

  jacoco {
    enabled = true
    includes = ['com/codeborne/selenide/**/*']
  }
  outputs.upToDateWhen {
    false
  }
}

task allTests(dependsOn: ['clean', 'check', 'test']) {}
